plugins {
    id "com.android.application"
    id "kotlin-android"
    id "kotlin-kapt"
    id "AndResGuard"
    id "io.michaelrocks.paranoid"
}

def keystoreProperties = new Properties()
try {
    keystoreProperties.load(new FileInputStream(rootProject.file("keystore.properties")))
} catch (Exception ignored) {
}
def applicationProperties = new Properties()
applicationProperties.load(new FileInputStream(rootProject.file("application.properties")))
def SHA1 = System.getenv("CIRCLE_SHA1")
def applicationVersionCode = applicationProperties["versionCode"].toInteger()
def applicationVersionName = applicationProperties["versionName"]
def isPerVersion = applicationProperties["isPerRelease"] == "true"
if (SHA1 != null) {
    applicationVersionName = applicationVersionName + ".${SHA1.substring(0, 7)}"
}
if (isPerVersion) {
    applicationVersionName = applicationVersionName + ".${applicationProperties["preReleaseName"]}-${applicationProperties["preReleaseVer"]}"
}

andResGuard {
    mappingFile = null
    use7zip = true
    useSign = true
    keepRoot = false
    mergeDuplicatedRes = true
    whiteList = ["R.mipmap.ic_launcher",
                 "R.mipmap.ic_launcher_foreground",
                 "R.mipmap.ic_launcher_round",
                 "R.drawable.ic_*",
                 "R.color.theme_color_*",
                 "R.id.*"]
    compressFilePattern = ["*.png",
                           "*.jpg",
                           "*.jpeg",
                           "*.gif",
                           "*.webp",
                           "*.kotlin_metadata",
                           "resources.arsc"]
    sevenzip {
        artifact = "com.tencent.mm:SevenZip:1.2.21"
    }
    finalApkBackupPath = "${project.rootDir}/app/release/${applicationVersionName}(${applicationVersionCode}).apk"
    digestalg = "SHA-256"
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"
    defaultConfig {
        applicationId "com.huanchengfly.tieba.post"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode applicationVersionCode
        versionName applicationVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            keyAlias keystoreProperties["releaseKeyAlias"]
            keyPassword keystoreProperties["releaseKeyPassword"]
            storeFile file(rootDir.getCanonicalPath() + "/" + keystoreProperties["releaseKeyStore"])
            storePassword keystoreProperties["releaseStorePassword"]
            enableV3Signing true
            enableV4Signing true
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    //Local Files
    implementation fileTree(include: ["*.jar"], dir: "libs")

    //滑动返回
    implementation "me.imid.swipebacklayout.lib:library:1.3.0"

    //AndroidX
    implementation "androidx.appcompat:appcompat:1.3.1"
    implementation "androidx.core:core-ktx:1.6.0"
    implementation "androidx.annotation:annotation:1.2.0"
    implementation "androidx.constraintlayout:constraintlayout:2.0.4"
    implementation "androidx.preference:preference-ktx:1.1.1"
    implementation "androidx.gridlayout:gridlayout:1.0.0"
    implementation "androidx.browser:browser:1.3.0"
    implementation "androidx.viewpager2:viewpager2:1.0.0"

    //Test
    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test:core:1.4.0"
    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation "androidx.test:rules:1.4.0"
    androidTestImplementation "androidx.test:runner:1.4.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"

    //Glide
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"
    implementation "com.github.bumptech.glide:okhttp3-integration:$glideVersion"

    implementation "com.google.android.material:material:1.4.0"

    implementation "com.squareup.okhttp3:okhttp:4.9.1"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.tsy:myokhttp:1.1.4"

    implementation "com.google.code.gson:gson:2.8.7"
    implementation "cn.dreamtobe.kpswitch:library:1.6.2"
    implementation "org.litepal.android:kotlin:3.0.0"
    implementation "com.github.SheHuan:RecyclerViewAdapter:1.2.9"
    implementation "com.lapism:searchview:28.0.0"
    implementation "com.github.lygttpod:SuperTextView:2.4.6"
    implementation "com.davemorrissey.labs:subsampling-scale-image-view:3.10.0"
    implementation "com.bm.photoview:library:2.0.7"
    implementation "cn.jzvd:jiaozivideoplayer:7.7.0"
    implementation "com.jrummyapps:colorpicker:2.1.7"
    implementation "com.scwang.wave:MultiWaveHeader:1.0.0-andx"

    implementation "com.zhihu.android:matisse:0.5.3-beta3"
    implementation "com.yanzhenjie:permission:2.0.3"
    implementation "com.gyf.immersionbar:immersionbar:3.0.0"
    implementation "com.flurry.android:analytics:12.13.0"
    implementation "androidx.palette:palette-ktx:1.0.0"

    implementation "com.github.yalantis:ucrop:2.2.7"

    implementation "com.billy.android:pre-loader:2.1.0"

    implementation "com.jakewharton:butterknife:10.2.3"
    kapt "com.jakewharton:butterknife-compiler:10.2.3"
}
